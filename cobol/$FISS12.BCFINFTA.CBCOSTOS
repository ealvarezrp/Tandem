?SUPPRESS
?ENV COMMON;RUNNAMED;COMPACT;SYMBOLS;INSPECT;SAVE ALL
?SAVEABEND
?SQL (RELEASE2,PAGES 900);SQLMEM EXT
?OPTIMIZE 2;NOWARN
*-----------------------------------------------------------------------------*
* PROCESO: Batch que genera archivo del Corrimiento de Costos entre Periodos
*-----------------------------------------------------------------------------*

 IDENTIFICATION  DIVISION.
 PROGRAM-ID. CBCOSTOS.
 AUTHOR. .

 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SPECIAL-NAMES.
     FILE #TERM                     IS HOME-TERM
     FILE $DCYC01.INFROJA0.LTALLIB  IS LTAL-LIB
     FILE "$SYSTEM.SYSTEM.COBOLLIB" IS COBOL-LIB.

 INPUT-OUTPUT SECTION.
 FILE-CONTROL.
 DATA DIVISION.
 FILE SECTION.

 WORKING-STORAGE SECTION.

 77 WSS-FILE-STATUS                   PIC X(02) VALUE SPACES.

 01 WSS-EOT-TABLA                  PIC X(01).
    88 WSS-EOT-TABLA-SI            VALUE "S".
    88 WSS-EOT-TABLA-NO            VALUE "N".

 01 WSS-PROG-ID.
    05 FILLER                   PIC  X(16)      VALUE "*** PROG FILE = ".
    05 W-PROG-FILE              PIC  X(18)      VALUE "CBCOSTOS".

 COPY WSS-VARS-STND-000 OF "$DESA12.SCOGLCPA.C85DEFPG".
 COPY ESS-VARS-STND-000 OF "$DESA12.SCOGLCPA.C85DEFPG".
 COPY ESS-VARS-STND-020 OF "$DESA12.SCOGLCPA.C85DEFPG".

 01 WSS-PARAM  PIC  X(30).
 01 WSS-RESULT PIC S9(4) COMP.

 01 WSS-PARAM-TXT-PERIODO PIC X(06).
 01 FILLER REDEFINES WSS-PARAM-TXT-PERIODO.
    03 WSS-PARAM-PERIODO PIC 9(06).

 01 WSS-PERIODO-POS PIC 9(06).
 01 WSS-PERIODO     PIC 9(06).
 01 WSS-PERIODO-RED REDEFINES WSS-PERIODO.
    02 WSS-YEAR  PIC 9(04).
    02 WSS-MONTH PIC 9(02).

 01 WSS-ARCHIVO PIC X(24) VALUE "$FISS00.BCFINDTA.LCOSTOS".

 01 WSS-ERROR-TAL                      PIC S9(04) COMP VALUE 0.

 01 WSS-FILENAME.
    05 WSS-FILENAME-FILE            PIC  X(24).

 01 WSS-FILE-LEN                       PIC S9(04) COMP VALUE 24.
 01 WSS-FILE-CODE                      PIC S9(04) COMP VALUE 0.
 01 WSS-PRIMARY                        NATIVE-2 VALUE 32000.
 01 WSS-SECONDARY                      NATIVE-2 VALUE 32000.
 01 WSS-MAXEXTENTS                     PIC S9(04) COMP VALUE 3200.
 01 WSS-FILETYPE                       PIC S9(04) COMP VALUE 0.
 01 WSS-RECORD                         PIC  9(05) COMP.
 01 WSS-BLOCK-LEN                      PIC S9(04) COMP VALUE 4096.

 01 WSS-FILE-NUM          PIC S9(04) COMP.
 01 WSS-ACCESS            PIC S9(04) COMP VALUE 2.
 01 WSS-EXCLUSION         PIC S9(04) COMP VALUE 0.

 01 WSS-REG.
    02 REG-NUM-CONTRATO          PIC 9(09).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-NUM-PRODUCTO          PIC 9(09).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-EMISORA               PIC X(24).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPP                   PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPPA                  PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPP-2                 PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPPA-2                PIC -9(16).9(2).
    02 FILLER                    PIC 9(04) COMP VALUE 3338.

 01 WSS-HEADER.
    02 FILLER           PIC X(08) VALUE "CONTRATO".
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(08) VALUE "PRODUCTO".
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(07) VALUE "EMISORA".
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(04) VALUE "CPP ".
    02 HEAD-PERIODO     PIC 9(06).
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(05) VALUE "CPPA".
    02 HEAD-PERIODO-2   PIC 9(06).
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(04) VALUE "CPP ".
    02 HEAD-POS         PIC 9(06).
    02 FILLER           PIC X(01) VALUE ",".
    02 FILLER           PIC X(05) VALUE "CPPA".
    02 HEAD-POS-2       PIC 9(06).
    02 FILLER           PIC 9(04) COMP VALUE 3338.

 01 WSS-FILE-EXIST       PIC X(01).
    88 WSS-FILE-EXIST-SI VALUE "S".
    88 WSS-FILE-EXIST-NO VALUE "N".

 01 WSS-AREA-TRAB.
     02 WSS-SALIDA-ERROR-CODE       PIC S9(04) COMP VALUE ZEROES.

 01 I            PIC 9(04) COMP.
 01 WSS-REC-LEN  PIC 9(04) COMP.

 EXTENDED-STORAGE SECTION.

 EXEC SQL INCLUDE SQLCA                       END-EXEC.
 EXEC SQL INCLUDE SQLSA                       END-EXEC.
 EXEC SQL BEGIN DECLARE SECTION               END-EXEC.
    EXEC SQL INVOKE =SREFISPR AS SREFISPR-REG END-EXEC
    01 H-EMISORA PIC X(24).
 EXEC SQL END  DECLARE SECTION                END-EXEC.

 EXEC SQL CONTROL QUERY INTERACTIVE ACCESS ON END-EXEC.

 EXEC SQL
   DECLARE CUR_SREFISPR CURSOR FOR
   SELECT
      RFPR_ANIO_MES
    , RFPR_NUM_CONTRATO
    , RFPR_NUM_PRODUCTO
    , RFPR_IMPO_COSTO_PROMEDIO
    , RFPR_IMPO_COSTO_PROM_AJUSTADO
    , TRIM(EMIS_CVE_TIPO_VALOR) || " " || TRIM(EMIS_DESC_VALOR) || " " || TRIM(EMIS_CVE_SERIE_VALOR)
   FROM =SREFISPR
   JOIN =SSUBSET ON RFPR_NUM_CONTRATO = SUBS_NUM_CONTRATO
   JOIN =SEMISORF ON RFPR_NUM_PRODUCTO = EMIS_NUM_PRODUCTO
   WHERE RFPR_ANIO_MES = :RFPR-ANIO-MES
   BROWSE ACCESS
 END-EXEC.

 01 WSS-TAB-BUFFER.
  02 ANY-TABLE OCCURS 250 TIMES.
*** IMPORTANTE LA LONGITUD EXACTA DEL REGISTRO
     03 WSS-A-BUFFER PIC X(130).

******************************************************************
*                       PROCEDURE DIVISION                       *
******************************************************************
 PROCEDURE DIVISION.

 0000-MAIN.
*==========
 PERFORM 0000-INICIO
 PERFORM 0000-PROCESO
 PERFORM 0000-FIN
 STOP RUN.

 0000-INICIO.
*============
 ENTER TAL "TGETMYID" OF LTAL-LIB
            USING        WSS-PARA-TGMID-PROGFILE
                         WSS-PARA-TGMID-PROCNAME
                         WSS-PARA-TGMID-USERGROUP
 PERFORM 0000-OBTIENE-PARAMETROS
 MOVE " <<CBCOSTOS>> Inicia Ejecucion "  TO ESS-MSG-OPER-L3-TXT-ERROR
 MOVE SPACES                             TO ESS-MSG-OPER-L4-TXT-ERROR
 PERFORM 8000000-MENSAJES-OPERADOR
 PERFORM 0000-BORRA-ARCHIVO
 PERFORM 0000-ABRE-ARCHIVO
 PERFORM 0000-ESCRIBE-HEADER
 INITIALIZE I WSS-RECORD.

 0000-ABRE-ARCHIVO.
*==================
 MOVE WSS-ARCHIVO TO WSS-FILENAME-FILE
 PERFORM 8000-FILE-EXIST
 IF WSS-FILE-EXIST-NO
    PERFORM 8000-FILE-CREATE
    PERFORM 8000-FILE-OPEN
 END-IF.

 0000-BORRA-ARCHIVO.
*===================
 MOVE WSS-ARCHIVO           TO WSS-FILENAME-FILE
 PERFORM 8000-FILE-EXIST
 IF WSS-FILE-EXIST-SI
    PERFORM 8000-FILE-PURGE
 END-IF.

 0000-ESCRIBE-HEADER.
*====================
 MOVE RFPR-ANIO-MES   TO HEAD-PERIODO HEAD-PERIODO-2
 MOVE WSS-PERIODO-POS TO HEAD-POS     HEAD-POS-2
 MOVE FUNCTION LENGTH(WSS-HEADER) TO WSS-RECORD
 ENTER "WRITEX"        USING WSS-FILE-NUM
                             WSS-HEADER
                             WSS-RECORD
                             OMITTED
                             OMITTED
 ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                             WSS-SALIDA-ERROR-CODE
                             OMITTED
                             OMITTED
                             OMITTED
                             OMITTED
 IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
    DISPLAY WSS-PROG-ID "WRITE FOR DATA-FILE"
            WSS-FILENAME
    DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
    ENTER TAL ABEND
 END-IF.

 0000-OBTIENE-PARAMETROS.
*========================
 MOVE ZEROES TO WSS-PARAM-PERIODO
 MOVE "PERIODO" TO WSS-PARAM.
 ENTER "GETPARAMTEXT" OF COBOL-LIB USING WSS-PARAM, WSS-PARAM-TXT-PERIODO
                                   GIVING WSS-RESULT
 IF WSS-RESULT < 1
    DISPLAY "ERROR: PROPORCIONE PARAMETRO PERIODO (aaaamm)"
    STOP RUN
 ELSE
   MOVE WSS-PARAM-PERIODO TO RFPR-ANIO-MES
   PERFORM 0000-OBTIENE-PERIODO-POS
*     PERFORM 0000-OBTIENE-PERIODO-ANT
 END-IF.

 0000-OBTIENE-PERIODO-ANT.
*=========================
 IF WSS-MONTH = 1
    MOVE 12 TO WSS-MONTH
    COMPUTE WSS-YEAR = WSS-YEAR - 1
 ELSE
   COMPUTE WSS-MONTH = WSS-MONTH - 1
 END-IF.

 0000-OBTIENE-PERIODO-POS.
*=========================
 MOVE WSS-PARAM-PERIODO TO WSS-PERIODO
 IF WSS-MONTH = 12
    MOVE 1 TO WSS-MONTH
    COMPUTE WSS-YEAR = WSS-YEAR + 1
 END-IF.
 MOVE WSS-PERIODO TO WSS-PERIODO-POS.

 0000-FIN.
*=========
 MOVE " <<CBCOSTOS>> Termina Ejecucion " TO ESS-MSG-OPER-L3-TXT-ERROR
 MOVE SPACES                          TO ESS-MSG-OPER-L4-TXT-ERROR
 PERFORM 8000000-MENSAJES-OPERADOR
 PERFORM 8000-FILE-CLOSE.

 0000-PROCESO.
*=============
 PERFORM 9000-OPEN-SREFISPR
 PERFORM UNTIL WSS-EOT-TABLA-SI
   PERFORM 9000-FETCH-SREFISPR
   IF WSS-EOT-TABLA-NO
      PERFORM 0000-LLENA-REGISTRO
      PERFORM 8000-WRITE
   END-IF
 END-PERFORM
 EXEC SQL CLOSE CUR_SREFISPR END-EXEC.
 IF I > ZEROES
    PERFORM 8000-FINAL-WRITE
 END-IF.

 0000-LLENA-REGISTRO.
*===================
 INITIALIZE WSS-REG
 MOVE RFPR-NUM-CONTRATO             TO REG-NUM-CONTRATO
 MOVE RFPR-NUM-PRODUCTO             TO REG-NUM-PRODUCTO
 MOVE H-EMISORA                     TO REG-EMISORA
 MOVE RFPR-IMPO-COSTO-PROMEDIO      TO REG-CPP  REG-CPP-2
 MOVE RFPR-IMPO-COSTO-PROM-AJUSTADO TO REG-CPPA REG-CPPA-2.

 9000-FETCH-SREFISPR.
*====================
 MOVE "N" TO WSS-EOT-TABLA
 EXEC SQL
   FETCH CUR_SREFISPR
   INTO :RFPR-ANIO-MES
       ,:RFPR-NUM-CONTRATO
       ,:RFPR-NUM-PRODUCTO
       ,:RFPR-IMPO-COSTO-PROMEDIO
       ,:RFPR-IMPO-COSTO-PROM-AJUSTADO
       ,:H-EMISORA
 END-EXEC.
 IF SQLCODE NOT = ZEROES
    MOVE "S" TO WSS-EOT-TABLA
    IF SQLCODE NOT = 100
       DISPLAY "ERROR EN SELECT-SREFISPR"
       DISPLAY "SQLCODE: " SQLCODE
       ENTER ABEND
    END-IF
 END-IF.

 9000-OPEN-SREFISPR.
*===================
 EXEC SQL
   OPEN CUR_SREFISPR
 END-EXEC.
 IF SQLCODE NOT = ZEROES
    DISPLAY "ERROR EN OPEN-SREFISPR"
    DISPLAY "SQLCODE: " SQLCODE
    ENTER ABEND
 END-IF.

 COPY MENSAJES-OPERADOR OF "$DESA12.SCOGLCPA.C85DEFPG".

 8000-CALL-TGETMYID.
      ENTER TAL "TGETMYID" OF LTAL-LIB USING
                                       WSS-PARA-TGMID-PROGFILE
                                       WSS-PARA-TGMID-PROCNAME
                                       WSS-PARA-TGMID-USERGROUP.


 8000-FILE-PURGE.
*================
 ENTER TAL "FILE_PURGE_" USING WSS-FILENAME
                         GIVING WSS-ERROR-TAL.

 8000-FILE-CREATE.
*==================
 MOVE ZEROES TO WSS-ERROR-TAL
 ENTER     "FILE_CREATE_" USING WSS-FILENAME,
                                WSS-FILE-LEN,
                                WSS-FILE-CODE,
                                WSS-PRIMARY,
                                WSS-SECONDARY,
                                WSS-MAXEXTENTS,
                                WSS-FILETYPE,
                                OMITTED,
                                OMITTED,
                                WSS-BLOCK-LEN
                         GIVING WSS-ERROR-TAL.
 IF WSS-ERROR-TAL NOT = ZEROES AND NOT = 10
    DISPLAY WSS-PROG-ID "FILE_CREATE_ FAILURE FOR "
            WSS-FILENAME
    DISPLAY WSS-PROG-ID "ERROR CODE " WSS-ERROR-TAL
    ENTER TAL ABEND
 END-IF.

 8000-FILE-OPEN.
*================
 MOVE ZEROES TO WSS-FILE-NUM
 ENTER "FILE_OPEN_" USING WSS-FILENAME(1:WSS-FILE-LEN)
                          WSS-FILE-NUM
                          WSS-ACCESS
                          WSS-EXCLUSION
                          OMITTED
                          OMITTED
                          OMITTED
                          OMITTED
                          OMITTED
                          OMITTED
                    GIVING WSS-SALIDA-ERROR-CODE
 IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
    DISPLAY WSS-PROG-ID "FILEOPEN FOR DATA-FILE"
            WSS-FILENAME
    DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

    ENTER TAL ABEND
 END-IF.

 8000-FILE-CLOSE.
*=================
 ENTER "FILE_CLOSE_" USING WSS-FILE-NUM
                           OMITTED
                    GIVING WSS-SALIDA-ERROR-CODE
 IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
    DISPLAY WSS-PROG-ID "FILECLOSE FOR DATA-FILE"
            WSS-FILENAME
    DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

    ENTER TAL ABEND
 END-IF.

 8000-WRITE.
*===========
 IF I < 250
    ADD 1                         TO I
    MOVE WSS-REG                  TO WSS-A-BUFFER (I)
    MOVE FUNCTION LENGTH(WSS-REG) TO WSS-REC-LEN
    ADD WSS-REC-LEN               TO WSS-RECORD.
 IF I = 250
    ENTER "WRITEX"        USING WSS-FILE-NUM
                                WSS-TAB-BUFFER
                                WSS-RECORD
                                OMITTED
                                OMITTED
    ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                                WSS-SALIDA-ERROR-CODE
                                OMITTED
                                OMITTED
                                OMITTED
                                OMITTED
    IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
       DISPLAY WSS-PROG-ID "WRITE FOR DATA-FILE"
               WSS-FILENAME
       DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
       ENTER TAL ABEND
    END-IF
    INITIALIZE I WSS-RECORD WSS-TAB-BUFFER
 END-IF.

 8000-FILE-EXIST.
*================
 ENTER "FILE_GETINFOBYNAME_" USING WSS-FILENAME
                    GIVING WSS-SALIDA-ERROR-CODE
 EVALUATE WSS-SALIDA-ERROR-CODE
    WHEN 11    MOVE "N" TO WSS-FILE-EXIST
    WHEN 0     MOVE "S" TO WSS-FILE-EXIST
    WHEN OTHER PERFORM 8000-ERROR
 END-EVALUATE.

 8000-ERROR.
*===========
 DISPLAY WSS-PROG-ID "FILE_GETINFOBYNAME"
         WSS-FILENAME
 DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
 ENTER TAL ABEND.

 8000-FINAL-WRITE.
*=================
 ENTER "WRITEX"        USING WSS-FILE-NUM
                             WSS-TAB-BUFFER
                             WSS-RECORD
 ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                             WSS-SALIDA-ERROR-CODE
 IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
    DISPLAY WSS-PROG-ID "WRITE FOR EDOCTA-FILE"
            WSS-FILENAME
    DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
    ENTER ABEND
 END-IF.

