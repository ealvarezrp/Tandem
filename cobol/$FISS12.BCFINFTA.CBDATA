?SUPPRESS
?ENV COMMON;HIGHPIN;RUNNAMED
?SAVE PARAM
?CHECK 15
?SQL (RELEASE2,PAGES 800);SQLMEM EXT
?INSPECT
?SYMBOLS
?SAVEABEND
?SAVE ALL

 IDENTIFICATION  DIVISION.
 PROGRAM-ID. CBRECOEX.
 AUTHOR.  FELIX NEVAREZ - ASISTICA.

 ENVIRONMENT DIVISION.
 CONFIGURATION SECTION.
 SPECIAL-NAMES.
      FILE "$SYSTEM.SYSTEM.COBOLLIB"   IS COBOL-LIB
      FILE "$DCYC01.EOPACOJA.CFFECHAS" IS CFFECHAS-LIB.

 INPUT-OUTPUT SECTION.
 FILE-CONTROL.

 DATA DIVISION.
 FILE SECTION.

 WORKING-STORAGE SECTION.

    EXEC SQL INCLUDE SQLCA                                    END-EXEC.
    EXEC SQL BEGIN DECLARE SECTION                            END-EXEC
    EXEC SQL INVOKE =SREFISTR AS SREFISTR-REG END-EXEC.
    EXEC SQL INVOKE =SREFISTC AS SREFISTC-REG END-EXEC.
    EXEC SQL INVOKE =SEMISORF AS SEMISORF-REG END-EXEC.
    EXEC SQL INVOKE =SCLAOPER AS SCLAOPER-REG END-EXEC.
    EXEC SQL INVOKE =SEMICPON AS SEMICPON-REG END-EXEC.
    EXEC SQL INVOKE =SREFISAC AS SREFISAC-REG END-EXEC.
    EXEC SQL INVOKE =SCLIENTE AS SCLIENTE-REG END-EXEC.
    EXEC SQL INVOKE =SCARXCTO AS SCARXCTO-REG END-EXEC.
    EXEC SQL INVOKE =SCONTRAF AS SCONTRAF-REG END-EXEC.
    EXEC SQL INVOKE =STRACOMP AS STRACOMP-REG END-EXEC.
    EXEC SQL INVOKE =SREFISPE AS SREFISPE-REG END-EXEC.
    EXEC SQL INVOKE =SHISTUNI AS SHISTUNI-REG END-EXEC.
    EXEC SQL INVOKE =SPERIOD  AS SPERIOD-REG  END-EXEC.
    EXEC SQL INVOKE =SREFISDO AS SREFISDO-REG  END-EXEC.
    EXEC SQL INVOKE =SOPDISVE AS SOPDISVE-REG  END-EXEC.
    EXEC SQL INVOKE =SHISTCAM AS SHISTCAM-REG  END-EXEC.
    EXEC SQL INVOKE =STRANSAF AS STRANSAF-REG  END-EXEC.
    01 H-HOSTS.
        02 H-REGIMEN-TRIB-INSTRUM       PIC X(07).
        02 H-REGIMEN-TRIB-CLIENTE       PIC X(07).
        02 H-PRECIO-TRANSACCION         PIC S9(16)V9(02).
        02 H-NUM-CTO-ORIGEN             PIC  9(09) COMP.
        02 H-COSTO-UNITARIO             PIC S9(10)V9(8) COMP.
        02 H-PRECEDENCIA                PIC  9(04) COMP.
        01 H-EJERCICIO                  PIC  9(04) COMP.
        01 H-INDICATOR                  PIC S9(04) COMP.
        01 H-IND-TIT                    PIC S9(04) COMP.
        01 H-NUMF-TRANSACCION           PIC  9(09) COMP.
        01 H-IMPO-RETENCION             PIC S9(16)V9(2) COMP.
        01 H-SUBSET                     PIC  X(01).
        01 H-FECHA                      PIC S9(08) COMP.
        01 H-CVE-OPER                   PIC  X(08).
    EXEC SQL END DECLARE SECTION                              END-EXEC.

 EXEC SQL CONTROL QUERY INTERACTIVE ACCESS OFF END-EXEC.

  EXEC SQL DECLARE CUR_SREFISTR CURSOR FOR
 SELECT RFTR_NUM_CONTRATO
 ,RFTR_NUM_PRODUCTO
 ,RFTR_FECH_FIN_OPERACION
 ,CASE WHEN RFTR_CVE_OPER="IMPUDEPA"
        OR RFTR_CVE_OPER="RETEIMPU"  THEN 1
      WHEN RFTR_CVE_OPER="AMORPACA"  THEN 3
      WHEN OPDI_CODN_TIPO_IMPORTE=4
       AND OPDI_CODN_CUS=0
       AND OPDI_CODN_EFE=1
       AND RFTR_CVE_OPER<>"AMORPACA" THEN 2
      WHEN RFTR_CVE_OPER="AMORDEPP"
        OR RFTR_CVE_OPER="AMORPATI"
        OR RFTR_CVE_OPER="AMORTIZA"  THEN 9
      ELSE 4 END
 ,RFTR_NUMF_TRANSACCION
 ,RFTR_ANIO_MES
 ,RFTR_CVE_TIPO_TRANSACCION
 ,RFTR_CVE_OPER
 ,RFTR_FECH_INI_OPERACION
 ,RFTR_TIT_TRANSACCION
 ,RFTR_IMPO_TRANSACCION
 ,RFTR_PLZO_DIAS
 ,RFTR_TASA_INT_PROM_NOMINAL
 ,RFTR_IMPO_INTERES_NOMINAL
 ,RFTR_IMPO_INTERES_REAL
 ,RFTR_PRECIO
 ,RFAC_NUM_PERSONA_PRIMER_TIT
 ,RFAC_ANIO_MES
 ,RFAC_NUM_USO_CONTRATO
 ,OPDI_CODN_TIPO_IMPORTE
 FROM =SREFISTR
 LEFT JOIN =SSUBSET ON RFTR_NUM_CONTRATO = SUBS_NUM_CONTRATO
 LEFT JOIN =SREFISAC ON RFTR_NUM_CONTRATO = RFAC_NUM_CONTRATO
 LEFT JOIN =SOPDISVE ON (RFTR_CVE_OPER,1)
         =(OPDI_CVE_OPER_ORIGEN,OPDI_NUMF_SECUENCIA_OPER)
 WHERE (OPDI_CODN_TIPO_IMPORTE NOT IN (1,2,3) OR OPDI_CODN_TIPO_IMPORTE IS NULL OR RFTR_CVE_OPER = "IMPUISR")
   AND CAST(RFTR_ANIO_MES/100 AS NUMERIC(4,0)) = :H-EJERCICIO
   AND RFAC_ANIO = :H-EJERCICIO
   AND RFTR_NUM_PRODUCTO > 0
   AND (RFTR_CVE_TIPO_SALDO IN ("REPO","DIRE") OR RFTR_CVE_TIPO_VALOR IN ("2","Q","R","R1","1C","51","52","FH"))
   AND RFAC_CVE_TIPO_CONTRATO NOT IN ("PR","IN","AG","PP")
   AND RFAC_NUM_USO_CONTRATO NOT IN (10,12,13,19,20,21,22)
   AND SUBSTRING(RFTR_CVE_OPER FROM 1 FOR 3) <> "IVA"
   AND RFTR_CVE_OPER NOT IN ("BONIIVA","BONIVAMC","COMIVAEF")
   AND (SUBS_NUM_CONTRATO IS NOT NULL OR :H-SUBSET = "N")
   AND RFTR_NUM_CONTRATO = 529772
 ORDER BY 1, 2, 3, 4, 5
 BROWSE ACCESS

  END-EXEC.

 77 W-FILE-STATUS                   PIC X(02) VALUE SPACES.

 01 WSS-PROG-ID.
    05 W-PROG-FILE              PIC  X(08)      VALUE "CBDATA".

 01 WSS-EOT-SREFISTR                   PIC X(01).
    88 WSS-EOT-SREFISTR-SI             VALUE "S".
    88 WSS-EOT-SREFISTR-NO             VALUE "N".
 01 WSS-EOT-SHISTUNI                   PIC X(01).
    88 WSS-EOT-SHISTUNI-SI             VALUE "S".
    88 WSS-EOT-SHISTUNI-NO             VALUE "N".
 01 WSS-EOT-SHISTCAM                   PIC X(01).
    88 WSS-EOT-SHISTCAM-SI             VALUE "S".
    88 WSS-EOT-SHISTCAM-NO             VALUE "N".

 01 WSS-ARCHIVO.
    02 FILLER PIC X(20) VALUE "$FISS00.BCFINDTA.DAT".
    02 WSS-ARCH-SUBSET PIC X(01).
    02 WSS-DATA-MES    PIC 99 VALUE ZEROES.
    02 WSS-DATA-DIA    PIC 99 VALUE ZEROES.

 01 WSS-ERROR-TAL                      PIC S9(04) COMP VALUE 0.

 01 WSS-FILENAME.
    05 WSS-FILENAME-FILE            PIC  X(25).

 01 WSS-FILE-LEN                       PIC S9(04) COMP VALUE 25.
 01 WSS-FILE-CODE                      PIC S9(04) COMP VALUE 0.
 01 WSS-PRIMARY                        NATIVE-2 VALUE 32000.
 01 WSS-SECONDARY                      NATIVE-2 VALUE 32000.
 01 WSS-MAXEXTENTS                     PIC S9(04) COMP VALUE 100.
 01 WSS-FILETYPE                       PIC S9(04) COMP VALUE 0.
 01 WSS-RECORD                         PIC  9(05) COMP.
 01 WSS-BLOCK-LEN                      PIC S9(04) COMP VALUE 4096.

 01 WSS-FILE-NUM          PIC S9(04) COMP.
 01 WSS-ACCESS            PIC S9(04) COMP VALUE 2.
 01 WSS-EXCLUSION         PIC S9(04) COMP VALUE 0.

 01 WSS-FMT-2     PIC -9(16).9(2).
 01 WSS-FMT-8     PIC -9(10).9(8).
 01 WSS-UDI       PIC S9(10)V9(8) COMP.
 01 WSS-IMPO      PIC S9(16)V9(2) COMP.
 01 WSS-IMPO-8    PIC S9(10)V9(8) COMP.
 01 WSS-INPC-INI  PIC S9(10)V9(8) COMP.
 01 WSS-INPC-FIN  PIC S9(10)V9(8) COMP.

 01 WSS-REG.
    02 REG-NUM-CONTRATO          PIC 9(09).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-DIVISA                PIC X(04).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-UNIDAD-VALUADORA      PIC X(04).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TIPO-CONTRATO         PIC X(07).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TIPO-PERSONA          PIC X(04).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CVE-OPER              PIC X(08).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-DESC-OPER             PIC X(50).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-NUM-PRODUCTO          PIC 9(09).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-DESC-VALOR            PIC X(10).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CVE-SERIE-VALOR       PIC X(08).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CVE-TIPO-VALOR        PIC X(04).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-FECH-INI-OPERACION    PIC X(08).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-FECH-FIN-OPERACION    PIC X(08).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TITULOS               PIC X(19).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-PRECIO-TRANSACCION    PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-IMPORTE               PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-PLZO-DIAS             PIC X(06).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TASA-RENDIMIENTO      PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-ISR-RETENIDO          PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPP                   PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-CPPA                  PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-COSTO-UNITARIO        PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-PRECIO-ADQUISICION    PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-PREG-NOMINAL          PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-GANANCIA-PERDIDA      PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-INT-NOMINAL-GRAVADO   PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-INT-NOMINAL-EXENTO    PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TOT-INT-NOMINAL       PIC -9(16).9(2).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-UDI-INICIO            PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-UDI-OPERACION         PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TIPO-CAMBIO-INICIO    PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-TIPO-CAMBIO-OPERACION PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-INPC-OPERACION        PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-INPC-INICIO           PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-FACTOR-ACTUALIZACION  PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-AJUSTE-X-INFLACION    PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-INTERES-REAL          PIC X(20).
    02 FILLER                    PIC X(01) VALUE ",".
    02 REG-NUM-FOLIO             PIC 9(09).
*     02 FILLER                    PIC X(01) VALUE ",".
     02 FILLER                    PIC X(01) VALUE " ".
     02 REG-NUM-GENERADORA        PIC ZZZZZZZZZ.
    02 FILLER                    PIC 9(04) COMP VALUE 3338.

 01 WSS-HEADER.
    02 FILLER PIC X(08) VALUE "CONTRATO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(06) VALUE "DIVISA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(16) VALUE "UNIDAD VALUADORA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(13) VALUE "TIPO CONTRATO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(12) VALUE "TIPO PERSONA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(15) VALUE "CLAVE OPERACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(23) VALUE "DESCRIPCION TRANSACCION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(08) VALUE "PRODUCTO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(07) VALUE "EMISORA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(05) VALUE "SERIE".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(10) VALUE "TIPO VALOR".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(12) VALUE "FECHA INICIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(17) VALUE "FECHA LIQUIDACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(07) VALUE "TITULOS".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(18) VALUE "PRECIO TRANSACCION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(07) VALUE "IMPORTE".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(10) VALUE "PLAZO PAGO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(16) VALUE "TASA RENDIMIENTO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(12) VALUE "ISR RETENIDO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(14) VALUE "COSTO PROMEDIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(23) VALUE "COSTO PROMEDIO AJUSTADO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(23) VALUE "COSTO PROMEDIO UNITARIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(18) VALUE "PRECIO ADQUISICION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(13) VALUE "VALOR NOMINAL".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(18) VALUE "GANANCIA O PERDIDA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(15) VALUE "INTERES NOMINAL".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(14) VALUE "INTERES EXENTO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(17) VALUE "TOTAL INT NOMINAL".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(10) VALUE "UDI INICIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(13) VALUE "UDI OPERACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(18) VALUE "TIPO CAMBIO INICIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(21) VALUE "TIPO CAMBIO OPERACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(16) VALUE "INPC LIQUIDACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(11) VALUE "INPC INICIO".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(23) VALUE "FACTOR DE ACTUALIZACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(20) VALUE "AJUSTE POR INFLACION".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(12) VALUE "INTERES REAL".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(05) VALUE "FOLIO".
*     02 FILLER PIC X(01) VALUE ",".
*     02 FILLER PIC X(10) VALUE "GENERADORA".
    02 FILLER PIC X(01) VALUE ",".
    02 FILLER PIC X(10) VALUE "          ".
    02 FILLER PIC 9(04) COMP VALUE 3338.

 01 WSS-FILE-EXIST       PIC X(01).
    88 WSS-FILE-EXIST-SI VALUE "S".
    88 WSS-FILE-EXIST-NO VALUE "N".

 01 WSS-AREA-TRAB.
     02 WSS-SALIDA-ERROR-CODE       PIC S9(04) COMP VALUE ZEROES.

 01 I            PIC 9(04) COMP.
 01 WSS-REC-LEN  PIC 9(04) COMP.
 01 WSS-ANIO-MES PIC 9(06) COMP.

 01 WSS-PARAM  PIC  X(30).
 01 WSS-RESULT PIC S9(4) COMP.

 01 WSS-PARAM-TXT-EJERCICIO PIC X(04).
 01 FILLER REDEFINES WSS-PARAM-TXT-EJERCICIO.
    03 WSS-PARAM-EJERCICIO PIC 9(04).

 01 WSS-PARAM-SUBSET PIC X(01).

 EXTENDED-STORAGE SECTION.

    01 WSS-TAB-BUFFER.
     02 ANY-TABLE OCCURS 40 TIMES.
*** IMPORTANTE LA LONGITUD EXACTA DEL REGISTRO
        03 WSS-A-BUFFER PIC X(656).

 01 WSS-FECHA PIC  9(08).
 01 WSS-DIAS  PIC S9(04) COMP.

 01 WSS-CURRENT-DATE-DATA.
   05  WSS-CURRENT-DATE.
       10  WSS-CURRENT-YEAR         PIC 9(04).
       10  WSS-CURRENT-MONTH        PIC 9(02).
       10  WSS-CURRENT-DAY          PIC 9(02).
   05  WSS-CURRENT-TIME.
       10  WSS-CURRENT-HOURS        PIC 9(02).
       10  WSS-CURRENT-MINUTE       PIC 9(02).
       10  WSS-CURRENT-SECOND       PIC 9(02).
       10  WSS-CURRENT-MILLISECONDS PIC 9(02).

 01 WSS-COSTO-FISCAL-TRASPASO PIC S9(16)V9(02).

 01 WSS-COSTO-UNITARIO        PIC S9(16)V9(2) COMP.

 01 WSS-FECH-INICIO PIC S9(8) COMP.

 01 WSS-FECHA-HOY.
    02 WSS-ANIO PIC 9(04).
    02 WSS-MES  PIC 9(02).
    02 WSS-DIA  PIC 9(02).
    02 FILLER   PIC X(13).

 01 LSS-CTO-TRATAMIENTO-IMPOSITIVO         PIC S9(4) VALUE ZERO COMP.
    88 CTO-GRAVADO                         VALUE 1.
    88 CTO-EXENTO                          VALUE 0.

 01 LSS-PRO-TRATAMIENTO-IMPOSITIVO         PIC S9(4) VALUE ZERO COMP.
    88 PRO-GRAVADO                         VALUE 1.
    88 PRO-EXENTO                          VALUE 0.

 01 LSS-TIPO-TRATAMIENTO                   PIC S9(4) VALUE ZERO COMP.
    88 ES-GRAVADO                          VALUE 1.
    88 ES-EXENTO                           VALUE 0.

 01 WSS-KEY.
    02 WSS-K-NUM-CONTRATO        PIC 9(09) COMP.
    02 WSS-K-NUM-PRODUCTO        PIC 9(09) COMP.
    02 WSS-K-FECH-FIN-OPERACION  PIC 9(08) COMP.
    02 WSS-K-CVE-OPER            PIC X(08).

 01 WSS-TR.
    02 WSS-T-NUM-CONTRATO        PIC 9(09) COMP.
    02 WSS-T-NUM-PRODUCTO        PIC 9(09) COMP.
    02 WSS-T-FECH-FIN-OPERACION  PIC 9(08) COMP.
    02 WSS-T-CVE-OPER            PIC X(08).

 PROCEDURE DIVISION.

 0000-MAIN.
*==========
 PERFORM 0000-INICIO
 PERFORM 1000-PROCESO
 PERFORM 0000-FIN
 STOP RUN.

 0000-FIN.
*=========
 PERFORM 8040-FILE-CLOSE
 MOVE FUNCTION CURRENT-DATE to WSS-CURRENT-DATE-DATA
 DISPLAY "FIN    DATA, " WSS-CURRENT-TIME.

 0000-INICIO.
*============
 MOVE FUNCTION CURRENT-DATE TO WSS-CURRENT-DATE-DATA
 MOVE WSS-CURRENT-MONTH     TO WSS-DATA-MES
 MOVE WSS-CURRENT-DAY       TO WSS-DATA-DIA
 PERFORM 0000-OBTIENE-PARAMETROS
 DISPLAY "INICIO DATA, " WSS-CURRENT-TIME.
 PERFORM 0010-BORRA-ARCHIVO
 PERFORM 0310-ABRE-ARCHIVO
 PERFORM ESCRIBE-HEADER.
 INITIALIZE I WSS-RECORD.

 0000-OBTIENE-PARAMETROS.
*========================
 MOVE ZEROES TO WSS-PARAM-EJERCICIO
 MOVE "EJERCICIO" TO WSS-PARAM.
 ENTER "GETPARAMTEXT" OF COBOL-LIB USING WSS-PARAM, WSS-PARAM-TXT-EJERCICIO
                                  GIVING WSS-RESULT
 IF WSS-RESULT < 1
    MOVE 2015 TO H-EJERCICIO
*     COMPUTE H-EJERCICIO = WSS-CURRENT-YEAR - 1
 ELSE
    MOVE WSS-PARAM-EJERCICIO TO H-EJERCICIO
 END-IF.
 MOVE "N"      TO WSS-PARAM-SUBSET
 MOVE "SUBSET" TO WSS-PARAM
 ENTER "GETPARAMTEXT" OF COBOL-LIB USING WSS-PARAM, WSS-PARAM-SUBSET
                        GIVING WSS-RESULT
 IF WSS-RESULT < 1 OR WSS-PARAM-SUBSET NOT = "S"
    MOVE "N" TO WSS-PARAM-SUBSET
 END-IF
 MOVE WSS-PARAM-SUBSET TO H-SUBSET
 IF H-SUBSET = "S"
    MOVE "X" TO WSS-ARCH-SUBSET
 ELSE
    MOVE "A" TO WSS-ARCH-SUBSET
 END-IF.

 0010-BORRA-ARCHIVO.
      MOVE WSS-ARCHIVO           TO WSS-FILENAME-FILE
      PERFORM 8060-FILE-EXIST
      IF WSS-FILE-EXIST-SI
         PERFORM 8000-FILE-PURGE
      END-IF.

 ESCRIBE-HEADER.
   MOVE FUNCTION LENGTH(WSS-HEADER) TO WSS-RECORD
      ENTER "WRITEX"        USING WSS-FILE-NUM
                                  WSS-HEADER
                                  WSS-RECORD
                                  OMITTED
                                  OMITTED
      ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                                  WSS-SALIDA-ERROR-CODE
                                  OMITTED
                                  OMITTED
                                  OMITTED
                                  OMITTED
      IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
         DISPLAY WSS-PROG-ID "WRITE FOR DATA-FILE"
                 WSS-FILENAME
         DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
         ENTER TAL ABEND
      END-IF.

 1000-PROCESO.
*=============
 INITIALIZE WSS-KEY
 PERFORM 9000-OPEN-CURSOR
 MOVE "N" TO WSS-EOT-SREFISTR
 PERFORM 0000-CICLO-SREFISTR UNTIL WSS-EOT-SREFISTR-SI
 PERFORM 9000-CLOSE-CURSOR
 IF I > ZEROES
     PERFORM 8000-FINAL-WRITE
 END-IF.

 9000-CLOSE-CURSOR.
      EXEC SQL CLOSE CUR_SREFISTR END-EXEC.

 8000-FINAL-WRITE.
      ENTER "WRITEX"        USING WSS-FILE-NUM
                                  WSS-TAB-BUFFER
                                  WSS-RECORD
      ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                                  WSS-SALIDA-ERROR-CODE
      IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
         DISPLAY WSS-PROG-ID "WRITE FOR EDOCTA-FILE"
                 WSS-FILENAME
         DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE
         ENTER ABEND
      END-IF.

 9000-OPEN-CURSOR.
    EXEC SQL OPEN CUR_SREFISTR END-EXEC
    IF SQLCODE NOT = ZEROES
        DISPLAY "ERROR EN OPEN-CURSOR"
        ENTER ABEND.

 0000-CICLO-SREFISTR.
*====================
 PERFORM 9000-FETCH-CURSOR
 IF WSS-EOT-SREFISTR-NO
    PERFORM 0000-PAGAINTE-MISMO-DIA
    PERFORM 9000-SELECT-SPERIOD
    PERFORM 0000-LLENA-REGISTRO
    PERFORM 8050-WRITE
 END-IF.

 0000-PAGAINTE-MISMO-DIA.
*========================
 IF RFTR-CVE-OPER = "PAGAINTE"
    MOVE RFTR-NUM-CONTRATO       TO WSS-T-NUM-CONTRATO
    MOVE RFTR-NUM-PRODUCTO       TO WSS-T-NUM-PRODUCTO
    MOVE RFTR-FECH-FIN-OPERACION TO WSS-T-FECH-FIN-OPERACION
    MOVE RFTR-CVE-OPER           TO WSS-T-CVE-OPER
    IF WSS-TR = WSS-KEY
       INITIALIZE RFTR-IMPO-INTERES-NOMINAL
                  RFTR-IMPO-INTERES-REAL
    END-IF
    MOVE WSS-TR TO WSS-KEY
 END-IF.


 0000-OBTIENE-UNIDADES.
*======================
 MOVE EMIS-CVE-UNIDAD-VALUADORA TO REG-DIVISA REG-UNIDAD-VALUADORA
 EVALUATE EMIS-CVE-UNIDAD-VALUADORA
    WHEN "UDI" PERFORM 0000-OBTIENE-UDIS
    WHEN "USD" PERFORM 0000-OBTIENE-DOLARES
    WHEN OTHER PERFORM 0000-OBTIENE-PESOS
 END-EVALUATE.

 0000-OBTIENE-PESOS.
*===================
 MOVE "INPC"          TO HUNI-CVE-INDICADOR-MERCADO
 MOVE WSS-FECH-INICIO TO WSS-FECHA
 PERFORM 0000-OBTIENE-INPC
 MOVE HUNI-PREG-INDICADOR TO WSS-INPC-INI
 MOVE WSS-INPC-INI        TO WSS-FMT-8
 MOVE WSS-FMT-8           TO REG-INPC-INICIO

 MOVE RFTR-FECH-FIN-OPERACION TO WSS-FECHA
 PERFORM 0000-OBTIENE-INPC
 MOVE HUNI-PREG-INDICADOR TO WSS-INPC-FIN
 MOVE WSS-INPC-FIN        TO WSS-FMT-8
 MOVE WSS-FMT-8           TO REG-INPC-OPERACION

 IF WSS-INPC-INI NOT = ZEROES
    COMPUTE WSS-IMPO-8 ROUNDED = WSS-INPC-FIN / WSS-INPC-INI
    MOVE WSS-IMPO-8 TO WSS-FMT-8
    MOVE WSS-FMT-8  TO REG-FACTOR-ACTUALIZACION

    COMPUTE WSS-IMPO ROUNDED = RFTC-IMPO-COSTO-PROMEDIO * WSS-IMPO-8
    MOVE WSS-IMPO  TO WSS-FMT-2
    MOVE WSS-FMT-2 TO REG-AJUSTE-X-INFLACION
 END-IF.

 0000-OBTIENE-UDIS.
*===================
 MOVE "UDI"           TO HUNI-CVE-INDICADOR-MERCADO
 MOVE WSS-FECH-INICIO TO HUNI-FECH-PUBLICACION-UNIDAD
 PERFORM 9000-SELECT-SHISTUNI
 MOVE HUNI-PREG-INDICADOR     TO WSS-FMT-8 WSS-UDI
 MOVE WSS-FMT-8               TO REG-UDI-INICIO

 MOVE RFTR-FECH-FIN-OPERACION TO HUNI-FECH-PUBLICACION-UNIDAD
 PERFORM 9000-SELECT-SHISTUNI
 MOVE HUNI-PREG-INDICADOR     TO WSS-FMT-8 WSS-UDI
 MOVE WSS-FMT-8               TO REG-UDI-OPERACION.

 0000-OBTIENE-DOLARES.
*=====================
 MOVE "USD"           TO HTCA-CVE-MONEDA
 MOVE WSS-FECH-INICIO TO HTCA-FECH-CIERRE
 PERFORM 9000-SELECT-SHISTCAM
 MOVE HTCA-PROP-TIPO-CAMBIO   TO WSS-FMT-8
 MOVE WSS-FMT-8               TO REG-TIPO-CAMBIO-INICIO

 MOVE RFTR-FECH-FIN-OPERACION TO HUNI-FECH-PUBLICACION-UNIDAD
 PERFORM 9000-SELECT-SHISTCAM
 MOVE HTCA-PROP-TIPO-CAMBIO   TO WSS-FMT-8
 MOVE WSS-FMT-8               TO REG-TIPO-CAMBIO-OPERACION.


 0000-OBTIENE-INPC.
*===================
 MOVE -1 TO WSS-DIAS
 CALL "DATEOFFSET" OF CFFECHAS-LIB USING WSS-FECHA, WSS-FECHA, WSS-DIAS
 MOVE WSS-FECHA TO HUNI-FECH-PUBLICACION-UNIDAD
 PERFORM 9000-SELECT-SHISTUNI.

 0000-LLENA-REGISTRO.
*====================
 INITIALIZE WSS-REG WSS-COSTO-UNITARIO
 PERFORM 0000-OBTIENE-FECHA-INICIO
 MOVE H-IMPO-RETENCION              TO REG-ISR-RETENIDO
 MOVE RFTR-NUM-CONTRATO             TO REG-NUM-CONTRATO
 MOVE RFTR-NUM-PRODUCTO             TO REG-NUM-PRODUCTO
 MOVE RFTR-NUMF-TRANSACCION         TO REG-NUM-FOLIO
 MOVE RFTC-NUMF-TRANS-GENERADORA    TO REG-NUM-GENERADORA
 MOVE ZEROES                        TO REG-NUM-GENERADORA
 MOVE EMIS-CVE-TIPO-VALOR           TO REG-CVE-TIPO-VALOR
 MOVE EMIS-DESC-VALOR               TO REG-DESC-VALOR
 MOVE EMIS-CVE-SERIE-VALOR          TO REG-CVE-SERIE-VALOR
 MOVE RFTC-IMPO-COSTO-PROMEDIO      TO REG-CPP
 MOVE RFTC-IMPO-COSTO-PROM-AJUSTADO TO REG-CPPA
 IF RFTR-TIT-TRANSACCION NOT = ZEROES
    COMPUTE WSS-COSTO-UNITARIO ROUNDED = RFTC-IMPO-COSTO-PROMEDIO / RFTR-TIT-TRANSACCION
 END-IF
 MOVE WSS-COSTO-UNITARIO   TO REG-COSTO-UNITARIO
 PERFORM 0000-TIPO-PERSONA
 PERFORM 0000-OBTIENE-UNIDADES
 PERFORM 0000-Revisa-Exencion-Imp
 IF CTO-GRAVADO
    MOVE "GRAVADO" TO REG-TIPO-CONTRATO
 ELSE
    MOVE "EXENTO"  TO REG-TIPO-CONTRATO
 END-IF
 PERFORM 0000-LLENA-TRANSACCION
 PERFORM 0000-OBTIENE-GANANCIA-PERDIDA
 IF RFTR-CVE-OPER = "ENTTITRA" OR "ENTRACO"
   PERFORM OBTIENE-ORIGEN
 END-IF.

 0000-Revisa-Exencion-Imp.
*=========================
 initialize lss-tipo-tratamiento
 perform 0000-Revisa-Imp-Cto.
 perform 0000-Revisa-Imp-Emi.
 set ES-EXENTO to TRUE
 if CTO-GRAVADO and PRO-GRAVADO
    set ES-GRAVADO to TRUE.

 0000-Revisa-Imp-Cto.
*====================
 initialize lss-cto-tratamiento-impositivo
            scarxcto-reg
            scontraf-reg
            scliente-reg
 move 1 to ccto-cant-porc-cargo
 move rftr-num-contrato to ccto-num-contrato
                           cont-num-contrato
 move "IMPU" to ccto-cve-cargo
 perform 9000-select-SCARXCTO
 move ccto-cant-porc-cargo
   to lss-cto-tratamiento-impositivo
 IF CTO-GRAVADO
    move 1 to ccto-cant-porc-cargo
    perform 9000-select-SCONTRAF
     move ccto-cant-porc-cargo
       to lss-cto-tratamiento-impositivo.
 move ZERO to SQLCODE.

 9000-select-SCARXCTO.
*=====================
 EXEC SQL
      SELECT CASE WHEN CCTO_CANT_PORC_CARGO=0 THEN 0 ELSE 1 END
       INTO :CCTO-CANT-PORC-CARGO
       FROM =SCARXCTO
       WHERE CCTO_NUM_CONTRATO=:CCTO-NUM-CONTRATO
         AND CCTO_CVE_CARGO=:CCTO-CVE-CARGO
       BROWSE ACCESS
 END-EXEC
 IF SQLCODE NOT = 100 and not = 0 and not = -8423
    DISPLAY  " Err " sqlcode " on SELECT =SCARXCTO "
             " contrato: " CONT-NUM-USO-CONTRATO.

 9000-select-SCONTRAF.
*=====================
 EXEC SQL
      SELECT CASE WHEN UPPER(TRIM(CLTE_CVE_ROL_CONTRATO))
             IN ("FIDU","FIHI")
               OR CONT_NUM_USO_CONTRATO IN (2,3,4,9)
                  THEN 0 ELSE 1 END
       INTO :CCTO-CANT-PORC-CARGO
       FROM =SCONTRAF
       LEFT JOIN =SCLIENTE ON
            (CONT_NUM_CONTRATO,CONT_NUM_PERSONA_PRIMER_TIT)
           =(CLTE_NUM_CONTRATO,CLTE_NUM_PERSONA)
       WHERE CONT_NUM_CONTRATO=:CONT-NUM-CONTRATO
       BROWSE ACCESS
 END-EXEC
 IF SQLCODE NOT = 100 and not = 0 and not = -8423
    DISPLAY  " Err " sqlcode " on SELECT =scontrat "
             " contrato: " CONT-NUM-USO-CONTRATO.

 0000-Revisa-Imp-Emi.
*====================
 initialize LSS-PRO-TRATAMIENTO-IMPOSITIVO
 if emis-band-cobra-impuestos-rv = "S" OR
    epon-band-cobra-impuestos-rv = "S"
    set PRO-GRAVADO to TRUE.
 move ZERO to SQLCODE.

 0000-TIPO-PERSONA.
*==================
 IF RFAC-NUM-USO-CONTRATO = 2 OR 3 OR 4 OR 9
    MOVE "FIDU" TO REG-TIPO-PERSONA
 ELSE
    PERFORM 9000-SELECT-SREFISDO
    IF RFDO-CVE-PAIS NOT = "MX"
       MOVE "EXTR" TO REG-TIPO-PERSONA
    ELSE
       MOVE RFPE-CVE-TIPO-PERSONA TO REG-TIPO-PERSONA
    END-IF
 END-IF.

 9000-SELECT-SREFISDO.
*=====================
 MOVE RFTR-ANIO-MES               TO RFDO-ANIO-MES
 MOVE RFTR-NUM-CONTRATO           TO RFDO-NUM-CONTRATO
 MOVE RFAC-NUM-PERSONA-PRIMER-TIT TO RFDO-NUM-PERSONA
 MOVE "FISC"                      TO RFDO-CODX-TIPO-DOMICILIO
 INITIALIZE RFDO-CVE-PAIS
 EXEC SQL
   SELECT RFDO_CVE_PAIS
   INTO  :RFDO-CVE-PAIS
   FROM =SREFISDO
   WHERE ( RFDO_ANIO_MES, RFDO_NUM_CONTRATO, RFDO_NUM_PERSONA, RFDO_CODX_TIPO_DOMICILIO)
       = (:RFDO-ANIO-MES,:RFDO-NUM-CONTRATO,:RFDO-NUM-PERSONA,:RFDO-CODX-TIPO-DOMICILIO)
   BROWSE ACCESS
 END-EXEC.



 OBTIENE-ORIGEN.
  INITIALIZE RFTC-NUMF-TRANS-GENERADORA H-NUM-CTO-ORIGEN H-COSTO-UNITARIO
  EXEC SQL
    SELECT RFTC_NUMF_TRANS_GENERADORA
    INTO :RFTC-NUMF-TRANS-GENERADORA
    FROM =SREFISTC
    WHERE RFTC_NUMF_TRANSACCION = :RFTR-NUMF-TRANSACCION
    BROWSE ACCESS
  END-EXEC.
  EXEC SQL
    SELECT RFTR_NUM_CONTRATO
    INTO :H-NUM-CTO-ORIGEN
    FROM =SREFISTR
    WHERE RFTR_NUMF_TRANSACCION = :RFTC-NUMF-TRANS-GENERADORA
    BROWSE ACCESS
  END-EXEC.
  EXEC SQL
      SELECT CASE WHEN RFSA_TIT_FINAL > 0
         THEN (RFSA_IMPO_COSTO_PROMEDIO + .00000001)
            / RFSA_TIT_FINAL
         ELSE 0 END
      INTO :H-COSTO-UNITARIO
      FROM =SREFISSA
      WHERE RFSA_NUMF_TRANSACCION = :RFTC-NUMF-TRANS-GENERADORA
      BROWSE ACCESS
  END-EXEC.
  COMPUTE WSS-COSTO-FISCAL-TRASPASO ROUNDED = H-COSTO-UNITARIO * RFTR-TIT-TRANSACCION
  IF (EMIS-CVE-UNIDAD-VALUADORA = "UDI" OR EPON-CVE-UNIDAD-VALUADORA = "UDI")
     COMPUTE WSS-COSTO-FISCAL-TRASPASO ROUNDED = WSS-COSTO-FISCAL-TRASPASO * WSS-UDI
  END-IF.

 0000-OBTIENE-GANANCIA-PERDIDA.
*==============================
 IF RFTR-CVE-OPER(1:4) = "AMOR" OR RFTR-CVE-OPER(1:3) = "VTA"
    COMPUTE WSS-IMPO ROUNDED = RFTR-IMPO-TRANSACCION - RFTC-IMPO-COSTO-PROMEDIO
    MOVE WSS-IMPO  TO WSS-FMT-2
    MOVE WSS-FMT-2 TO REG-GANANCIA-PERDIDA
 END-IF.

 0000-LLENA-TRANSACCION.
*=======================
 INITIALIZE H-PRECIO-TRANSACCION
 MOVE RFTR-CVE-OPER                TO REG-CVE-OPER
 MOVE CVEO-DESC-LARGA              TO REG-DESC-OPER
 MOVE RFTR-FECH-FIN-OPERACION      TO REG-FECH-FIN-OPERACION
 MOVE RFTR-TIT-TRANSACCION         TO REG-TITULOS
 MOVE RFTR-PLZO-DIAS               TO REG-PLZO-DIAS
 IF RFTR-TIT-TRANSACCION NOT = ZEROES
    COMPUTE H-PRECIO-TRANSACCION ROUNDED = RFTR-IMPO-TRANSACCION / RFTR-TIT-TRANSACCION
 END-IF
 MOVE RFTR-IMPO-TRANSACCION        TO WSS-FMT-2
 MOVE WSS-FMT-2                    TO REG-IMPORTE
 MOVE H-PRECIO-TRANSACCION          TO WSS-FMT-2
 MOVE WSS-FMT-2                    TO REG-PRECIO-TRANSACCION
 MOVE RFTR-PRECIO                  TO WSS-FMT-2
 MOVE WSS-FMT-2                    TO REG-PRECIO-ADQUISICION
 MOVE EMIS-PREG-NOMINAL            TO WSS-FMT-8
 MOVE WSS-FMT-8                    TO REG-PREG-NOMINAL
 MOVE RFTR-TASA-INT-PROM-NOMINAL   TO WSS-FMT-8
 MOVE WSS-FMT-8                    TO REG-TASA-RENDIMIENTO
 IF ES-GRAVADO
    MOVE RFTR-IMPO-INTERES-NOMINAL TO WSS-FMT-2
    MOVE WSS-FMT-2                 TO REG-INT-NOMINAL-GRAVADO
                                      REG-TOT-INT-NOMINAL
 ELSE
    MOVE RFTR-IMPO-INTERES-NOMINAL TO WSS-FMT-2
    MOVE WSS-FMT-2                 TO REG-INT-NOMINAL-EXENTO
                                      REG-TOT-INT-NOMINAL
 END-IF
 MOVE RFTR-IMPO-INTERES-REAL       TO WSS-FMT-2
 MOVE WSS-FMT-2                    TO REG-INTERES-REAL.

 0000-OBTIENE-FECHA-INICIO.
*==========================
 INITIALIZE WSS-FECH-INICIO
 EVALUATE RFTR-CVE-OPER
    WHEN "CPRADIRE" MOVE RFTR-FECH-FIN-OPERACION TO WSS-FECH-INICIO
    WHEN "PAGAINTE" PERFORM 0000-FECHA-INICIO-PAGAINTE
    WHEN "AMORPATI" PERFORM 0000-FECHA-INICIO-PAGAINTE
    WHEN OTHER      PERFORM 0000-FECHA-INICIO
 END-EVALUATE.
 MOVE WSS-FECH-INICIO TO REG-FECH-INI-OPERACION.

 0000-FECHA-INICIO-PAGAINTE.
*===========================
 MOVE "PAGAINTE" TO H-CVE-OPER
 PERFORM 9000-SELECT-SREFISTR
 IF SQLCODE = ZEROES
    MOVE H-FECHA TO WSS-FECH-INICIO
 ELSE
    MOVE "CPRADIRE" TO H-CVE-OPER
    PERFORM 9000-SELECT-SREFISTR
    IF SQLCODE = ZEROES
       MOVE H-FECHA TO WSS-FECH-INICIO
    END-IF
 END-IF.

 9000-SELECT-SREFISTR.
*=====================
 EXEC SQL
    SELECT MAX(RFTR_FECH_FIN_OPERACION)
    INTO :H-FECHA
    FROM =SREFISTR
    WHERE RFTR_NUM_CONTRATO     = :RFTR-NUM-CONTRATO
      AND RFTR_NUM_PRODUCTO     = :RFTR-NUM-PRODUCTO
      AND RFTR_CVE_OPER         = :H-CVE-OPER
      AND RFTR_FECH_FIN_OPERACION  < :RFTR-FECH-FIN-OPERACION
    BROWSE ACCESS
 END-EXEC.

 0000-FECHA-INICIO.
*==================
 IF RFTC-FECH-ULT-MOVTO = ZEROES
    MOVE RFTR-FECH-INI-OPERACION TO WSS-FECH-INICIO
 ELSE
    MOVE RFTC-FECH-ULT-MOVTO     TO WSS-FECH-INICIO
 END-IF.

 0310-ABRE-ARCHIVO.
      MOVE WSS-ARCHIVO           TO WSS-FILENAME-FILE
      PERFORM 8060-FILE-EXIST
      IF WSS-FILE-EXIST-NO
         PERFORM 8010-FILE-CREATE
         PERFORM 8030-FILE-OPEN.

 8000-FILE-PURGE.
      ENTER TAL "FILE_PURGE_" USING WSS-FILENAME
                              GIVING WSS-ERROR-TAL.

 8010-FILE-CREATE.
      MOVE ZEROES TO WSS-ERROR-TAL
      ENTER     "FILE_CREATE_" USING WSS-FILENAME,
                                     WSS-FILE-LEN,
                                     WSS-FILE-CODE,
                                     WSS-PRIMARY,
                                     WSS-SECONDARY,
                                     WSS-MAXEXTENTS,
                                     WSS-FILETYPE,
                                     OMITTED,
                                     OMITTED,
                                     WSS-BLOCK-LEN
                              GIVING WSS-ERROR-TAL.
      IF WSS-ERROR-TAL NOT = ZEROES AND NOT = 10
         DISPLAY WSS-PROG-ID "FILE_CREATE_ FAILURE FOR "
                 WSS-FILENAME
         DISPLAY WSS-PROG-ID "ERROR CODE " WSS-ERROR-TAL
         ENTER TAL ABEND.

 8030-FILE-OPEN.
      MOVE ZEROES TO WSS-FILE-NUM
      ENTER "FILE_OPEN_" USING WSS-FILENAME(1:WSS-FILE-LEN)
                               WSS-FILE-NUM
                               WSS-ACCESS
                               WSS-EXCLUSION
                               OMITTED
                               OMITTED
                               OMITTED
                               OMITTED
                               OMITTED
                               OMITTED
                         GIVING WSS-SALIDA-ERROR-CODE
      IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
         DISPLAY WSS-PROG-ID "FILEOPEN FOR DATA-FILE"
                 WSS-FILENAME
         DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

         ENTER TAL ABEND.

 8040-FILE-CLOSE.
      ENTER "FILE_CLOSE_" USING WSS-FILE-NUM
                                OMITTED
                         GIVING WSS-SALIDA-ERROR-CODE
      IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
         DISPLAY WSS-PROG-ID "FILECLOSE FOR DATA-FILE"
                 WSS-FILENAME
         DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

         ENTER TAL ABEND.

 8050-WRITE.
     IF I < 40
       ADD 1                         TO I
       MOVE WSS-REG                  TO WSS-A-BUFFER (I)
       MOVE FUNCTION LENGTH(WSS-REG) TO WSS-REC-LEN
       ADD WSS-REC-LEN               TO WSS-RECORD.
     IF I = 40
          ENTER "WRITEX"        USING WSS-FILE-NUM
                                      WSS-TAB-BUFFER
                                      WSS-RECORD
                                      OMITTED
                                      OMITTED
          ENTER "FILE_GETINFO_" USING WSS-FILE-NUM
                                      WSS-SALIDA-ERROR-CODE
                                      OMITTED
                                      OMITTED
                                      OMITTED
                                      OMITTED
          IF WSS-SALIDA-ERROR-CODE NOT = ZEROES
             DISPLAY WSS-PROG-ID "WRITE FOR DATA-FILE"
                     WSS-FILENAME
             DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

             ENTER TAL ABEND
          END-IF
          INITIALIZE I WSS-RECORD WSS-TAB-BUFFER.

 8060-FILE-EXIST.
      ENTER "FILE_GETINFOBYNAME_" USING WSS-FILENAME
                         GIVING WSS-SALIDA-ERROR-CODE
      EVALUATE WSS-SALIDA-ERROR-CODE
         WHEN 11    MOVE "N" TO WSS-FILE-EXIST
         WHEN 0     MOVE "S" TO WSS-FILE-EXIST
         WHEN OTHER PERFORM 8065-ERROR
      END-EVALUATE.

 8065-ERROR.
        DISPLAY WSS-PROG-ID "FILE_GETINFOBYNAME"
                 WSS-FILENAME
        DISPLAY WSS-PROG-ID "ERROR CODE " WSS-SALIDA-ERROR-CODE

        ENTER TAL ABEND.

 9000-SELECT-SHISTUNI.
*=====================
 MOVE "N" TO WSS-EOT-SHISTUNI
 INITIALIZE HUNI-PREG-INDICADOR
 EXEC SQL
     SELECT HUNI_PREG_INDICADOR
     INTO :HUNI-PREG-INDICADOR
     FROM =SHISTUNI
     WHERE HUNI_CVE_INDICADOR_MERCADO   = :HUNI-CVE-INDICADOR-MERCADO
       AND HUNI_FECH_PUBLICACION_UNIDAD = :HUNI-FECH-PUBLICACION-UNIDAD
     BROWSE ACCESS
 END-EXEC.
 IF SQLCODE NOT = ZEROES
     MOVE "S" TO WSS-EOT-SHISTUNI
     IF SQLCODE NOT = 100
         DISPLAY "ERROR EN SELECT-SHISTUNI"
         ENTER ABEND.

 9000-SELECT-SHISTCAM.
*=====================
 MOVE "N" TO WSS-EOT-SHISTCAM
 INITIALIZE HTCA-PROP-TIPO-CAMBIO
 EXEC SQL
     SELECT HTCA_PROP_TIPO_CAMBIO
     INTO  :HTCA-PROP-TIPO-CAMBIO
     FROM =SHISTCAM
     WHERE HTCA_FECH_CIERRE   = :HTCA-FECH-CIERRE
       AND HTCA_CVE_MONEDA    = :HTCA-CVE-MONEDA
     BROWSE ACCESS
 END-EXEC.
 IF SQLCODE NOT = ZEROES
     MOVE "S" TO WSS-EOT-SHISTCAM
     IF SQLCODE NOT = 100
         DISPLAY "ERROR EN SELECT-SHISTCAM"
         DISPLAY "FECHA: "  HTCA-FECH-CIERRE
         DISPLAY "MONEDA: " HTCA-CVE-MONEDA.

 9000-FETCH-CURSOR.
      PERFORM 9000-FETCH-SREFISTR
      IF WSS-EOT-SREFISTR-NO
         INITIALIZE SEMISORF-REG
         MOVE RFTR-NUM-PRODUCTO TO EMIS-NUM-PRODUCTO
                                   EPON-NUM-PRODUCTO
         MOVE RFTR-NUM-CONTRATO TO CCTO-NUM-CONTRATO
         MOVE "IMPU"            TO CCTO-CVE-CARGO
         MOVE RFTR-CVE-OPER     TO CVEO-CVE-OPER
         PERFORM 9000-SELECT-SEMISORF
         PERFORM 9000-SELECT-SEMICPON
         PERFORM 9000-SELECT-SCLAOPER
         PERFORM 9000-SELECT-SREFISTC
         PERFORM 9000-SELECT-SREFISPE
         IF H-IND-TIT = -1
            PERFORM 9000-SELECT-STRACOMP
         END-IF
         PERFORM 0000-OBTIENE-RETENCION
      END-IF.

 0000-OBTIENE-RETENCION.
*=======================
 INITIALIZE H-IMPO-RETENCION
*** Obtiene folio de la retencion
 EXEC SQL
     SELECT TRAN_IMPO_TRANSACCION
     INTO :H-IMPO-RETENCION
     FROM =STRANSAF
     LEFT JOIN =SOPDISVE ON (TRAN_CVE_OPER,1)
         =(OPDI_CVE_OPER_ORIGEN,OPDI_NUMF_SECUENCIA_OPER)
     WHERE TRAN_NUMF_TRANS_GENERADORA = :RFTC-NUMF-TRANS-GENERADORA
       AND TRAN_NUMF_TRANSACCION <> :RFTC-NUMF-TRANS-GENERADORA
       AND TRAN_FECH_APLICACION = :RFTR-FECH-FIN-OPERACION
       AND TRAN_CODX_STATUS = "ACTI"
       AND OPDI_CODN_TIPO_IMPORTE = 1
     BROWSE ACCESS
 END-EXEC.
 IF SQLCODE NOT = ZEROES AND NOT = 100
    DISPLAY "ERROR EN OBTIENE-RETENCION"
    ENTER ABEND
 END-IF.

*  0000-OBTIENE-RETENCION.
* *=======================
*  INITIALIZE H-IMPO-RETENCION
* *** Obtiene folio de la retencion
*  EXEC SQL
*      SELECT RFTC_NUMF_TRANSACCION
*      INTO :H-NUMF-TRANSACCION
*      FROM =SREFISTC
*      WHERE RFTC_NUMF_TRANS_GENERADORA = :RFTC-NUMF-TRANS-GENERADORA
*        AND RFTC_NUMF_TRANSACCION <> :RFTC-NUMF-TRANS-GENERADORA
*      BROWSE ACCESS
*  END-EXEC.
*  IF SQLCODE = ZEROES
*     EXEC SQL
*         SELECT RFTR_IMPO_TRANSACCION
*         INTO :H-IMPO-RETENCION
*         FROM =SREFISTR
*         LEFT JOIN =SOPDISVE ON (RFTR_CVE_OPER,1)
*             =(OPDI_CVE_OPER_ORIGEN,OPDI_NUMF_SECUENCIA_OPER)
*         WHERE RFTR_NUMF_TRANSACCION = :H-NUMF-TRANSACCION
*           AND OPDI_CODN_TIPO_IMPORTE = 1
*         BROWSE ACCESS
*     END-EXEC
*  END-IF.

 9000-FETCH-SREFISTR.
*====================
 INITIALIZE SREFISTR-REG H-HOSTS SCLAOPER-REG SREFISPE-REG SREFISTC-REG SOPDISVE-REG
 MOVE "N" TO WSS-EOT-SREFISTR
 EXEC SQL
     FETCH CUR_SREFISTR INTO
      :RFTR-NUM-CONTRATO
     ,:RFTR-NUM-PRODUCTO
     ,:RFTR-FECH-FIN-OPERACION
     ,:H-PRECEDENCIA
     ,:RFTR-NUMF-TRANSACCION
     ,:RFTR-ANIO-MES
     ,:RFTR-CVE-TIPO-TRANSACCION
     ,:RFTR-CVE-OPER
     ,:RFTR-FECH-INI-OPERACION
     ,:RFTR-TIT-TRANSACCION         INDICATOR :H-IND-TIT
     ,:RFTR-IMPO-TRANSACCION
     ,:RFTR-PLZO-DIAS
     ,:RFTR-TASA-INT-PROM-NOMINAL
     ,:RFTR-IMPO-INTERES-NOMINAL
     ,:RFTR-IMPO-INTERES-REAL
     ,:RFTR-PRECIO
     ,:RFAC-NUM-PERSONA-PRIMER-TIT
     ,:RFAC-ANIO-MES
     ,:RFAC-NUM-USO-CONTRATO
     ,:OPDI-CODN-TIPO-IMPORTE       INDICATOR :H-INDICATOR
 END-EXEC.
 IF SQLCODE NOT = ZEROES
     MOVE "S" TO WSS-EOT-SREFISTR
     IF SQLCODE NOT = 100
         DISPLAY "ERROR EN FETCH-SREFISTR"
         ENTER SQLCADISPLAY USING SQLCA
         ENTER ABEND
     END-IF
 END-IF.

 9000-SELECT-SPERIOD.
      INITIALIZE PERI-TASA-RESULTANTE
      EXEC SQL
        SELECT PERI_TASA_RESULTANTE
        INTO :PERI-TASA-RESULTANTE
        FROM $DESA2.RF0914.SPERIOD
        WHERE PERI_NUM_PRODUCTO = :RFTR-NUM-PRODUCTO
          AND PERI_FECH_PAGO_CUPON = :RFTR-FECH-FIN-OPERACION
        BROWSE ACCESS
      END-EXEC.

 9000-SELECT-SEMISORF.
      EXEC SQL
          SELECT EMIS_CVE_TIPO_VALOR
                 ,EMIS_DESC_VALOR
                 ,EMIS_CVE_SERIE_VALOR
                ,CASE WHEN EMIS_CVE_UNIDAD_VALUADORA = "" THEN "MXN" ELSE EMIS_CVE_UNIDAD_VALUADORA END
                ,EMIS_PREG_NOMINAL
                ,EMIS_BAND_COBRA_IMPUESTOS_RV
          INTO :EMIS-CVE-TIPO-VALOR
              ,:EMIS-DESC-VALOR
              ,:EMIS-CVE-SERIE-VALOR
              ,:EMIS-CVE-UNIDAD-VALUADORA
              ,:EMIS-PREG-NOMINAL
              ,:EMIS-BAND-COBRA-IMPUESTOS-RV
          FROM =SEMISORF
          WHERE EMIS_NUM_PRODUCTO = :EMIS-NUM-PRODUCTO
          BROWSE ACCESS
      END-EXEC.

 9000-SELECT-SEMICPON.
      INITIALIZE EPON-CVE-UNIDAD-VALUADORA EPON-BAND-COBRA-IMPUESTOS-RV
      EXEC SQL
          SELECT EPON_CVE_UNIDAD_VALUADORA
                ,EPON_BAND_COBRA_IMPUESTOS_RV
          INTO :EPON-CVE-UNIDAD-VALUADORA
              ,:EPON-BAND-COBRA-IMPUESTOS-RV
          FROM =SEMICPON
          WHERE EPON_NUM_PRODUCTO = :EPON-NUM-PRODUCTO
          BROWSE ACCESS
      END-EXEC.

 9000-SELECT-SCLAOPER.
      INITIALIZE CVEO-DESC-LARGA
      EXEC SQL
          SELECT CVEO_DESC_LARGA
          INTO :CVEO-DESC-LARGA
          FROM =SCLAOPER
          WHERE CVEO_CVE_OPER = :CVEO-CVE-OPER
          BROWSE ACCESS
      END-EXEC.

 9000-SELECT-SREFISTC.
*=====================
 INITIALIZE SREFISTC-REG
 EXEC SQL
     SELECT RFTC_NUMF_TRANS_GENERADORA
           ,RFTC_FECH_ULT_MOVTO
           ,RFTC_IMPO_COSTO_PROMEDIO
           ,RFTC_IMPO_COSTO_PROM_AJUSTADO
     INTO :RFTC-NUMF-TRANS-GENERADORA
         ,:RFTC-FECH-ULT-MOVTO
         ,:RFTC-IMPO-COSTO-PROMEDIO
         ,:RFTC-IMPO-COSTO-PROM-AJUSTADO
     FROM =SREFISTC
     WHERE RFTC_NUMF_TRANSACCION = :RFTR-NUMF-TRANSACCION
     BROWSE ACCESS
 END-EXEC.

 9000-SELECT-STRACOMP.
*=====================
 INITIALIZE STRACOMP-REG
 EXEC SQL
     SELECT TRCO_TIT_TRANSACCION
     INTO :RFTR-TIT-TRANSACCION
     FROM =STRACOMP
     WHERE TRCO_NUMF_TRANSACCION = :RFTR-NUMF-TRANSACCION
     BROWSE ACCESS
 END-EXEC.

 9000-SELECT-SREFISPE.
*=====================
 INITIALIZE SREFISPE-REG
 EXEC SQL
     SELECT RFPE_CVE_TIPO_PERSONA
     INTO  :RFPE-CVE-TIPO-PERSONA
     FROM =SREFISPE
     WHERE ( RFPE_ANIO_MES,  RFPE_NUM_CONTRATO,  RFPE_NUM_PERSONA)
         = (:RFAC-ANIO-MES, :RFTR-NUM-CONTRATO, :RFAC-NUM-PERSONA-PRIMER-TIT)
     BROWSE ACCESS
 END-EXEC.
